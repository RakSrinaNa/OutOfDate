buildscript {
    ext {
        slf4j_version = '2.0.0-alpha1'
        logback_version = '1.3.0-alpha5'
        raksrinana_utils_version = '15.0.0'
        picocli_version = '4.5.1'
        hibernate_version = '5.4.21.Final'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.ben-manes.versions" version '0.28.0'
    id 'com.novoda.build-properties' version '0.4.1'
    id 'idea'
    id 'java'
    id 'application'
    id "io.freefair.lombok" version '5.0.1'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

def secretsFile = new File(".", "secrets.properties")

if (!secretsFile.getParentFile().exists()) {
    secretsFile.mkdirs()
}

if (!secretsFile.exists()) {
    secretsFile.withWriterAppend { w ->
        w << "\n"
    }
}

buildProperties {
    secrets {
        using(file('secrets.properties')).or(System.getenv()).or([
                "GITHUB_USER" : "RakSrinaNa",
                "GITHUB_TOKEN": "N/A"
        ])
    }
}

group = 'fr.raksrinana'
description = 'OutOfDate'
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_15

mainClassName = project.mainClass

dependencies {
    implementation("org.slf4j:slf4j-api:${slf4j_version}")
    implementation("ch.qos.logback:logback-classic:${logback_version}") {
        exclude group: 'edu.washington.cs.types.checker', module: 'checker-framework'
    }
    implementation("fr.raksrinana:utils.javafx:${raksrinana_utils_version}")
    implementation("fr.raksrinana:utils.http:${raksrinana_utils_version}")

    implementation("info.picocli:picocli:${picocli_version}")

    implementation("org.hibernate:hibernate-agroal:${hibernate_version}")
}

javafx {
    version = project.sourceCompatibility.getName()
    modules = ['javafx.base', 'javafx.graphics', 'javafx.controls']
}

repositories {
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://jcenter.bintray.com'
    }
    maven {
        url = 'https://maven.pkg.github.com/RakSrinaNa/JavaUtils/'
        credentials {
            username buildProperties.secrets['GITHUB_USER'].string
            password buildProperties.secrets['GITHUB_TOKEN'].string
        }
    }
}

processResources {
    expand project.properties
}

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

application {
    mainModule = project.moduleName
    mainClass = project.mainClass
}

java {
    modularity.inferModulePath.set(true)
}

shadowJar {
    archivesBaseName = project.name
    classifier = 'shaded'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.deprecation = true
}

tasks.withType(Wrapper) {
    gradleVersion = project.wrapper_version
}
